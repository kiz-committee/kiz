(* ========================================================== *)
(* Kiz Programming Language - Syntax BNF Specification        *)
(* Author: azhz1107cat                                        *)
(* ========================================================== *)

(* 入口 *)
program             ::= ( statement | function_def | object_def )*

(* 语句 *)
statement           ::= empty_stmt
                      | assign_stmt
                      | expr_stmt
                      | if_stmt
                      | while_stmt
                      | for_stmt
                      | break_stmt
                      | next_stmt
                      | return_stmt
                      | throw_stmt
                      | try_stmt
                      | ensure_stmt
                      | import_stmt
                      | global_stmt
                      | nonlocal_stmt

empty_stmt          ::= ';'  // 可选空语句

(* 注释 - 词法级，这里仅标注 *)
comment_line        ::= '#' .*? '\n'
comment_block       ::= '/*' .*? '*/'

(* -------------------------------------------------------------------------- *)
(* 变量与赋值 *)
(* -------------------------------------------------------------------------- *)
assign_stmt         ::= identifier '=' expr
                      | primary '[' expr ']' '=' expr       // a[b] = c
                      | primary '.' identifier '=' expr     // a.b = c

global_stmt         ::= 'global' identifier '=' expr
nonlocal_stmt       ::= 'nonlocal' identifier '=' expr

(* -------------------------------------------------------------------------- *)
(* 表达式 *)
(* -------------------------------------------------------------------------- *)
expr_stmt           ::= expr

expr                ::= logic_or_expr

logic_or_expr       ::= logic_and_expr ( 'or' logic_and_expr )*
logic_and_expr      ::= equality_expr ( 'and' equality_expr )*

equality_expr       ::= relational_expr ( ( '==' | '!=' ) relational_expr )*
relational_expr      ::= additive_expr ( ( '<' | '>' | '<=' | '>=' | 'in' | 'is' ) additive_expr )*

additive_expr       ::= multiplicative_expr ( ( '+' | '-' ) multiplicative_expr )*
multiplicative_expr ::= unary_expr ( ( '*' | '/' | '%' | '^' ) unary_expr )*

unary_expr          ::= ( '+' | '-' | 'not' ) primary
                      | primary

primary             ::= literal
                      | identifier
                      | list_literal
                      | dict_literal
                      | lambda_expr
                      | '(' expr ')'
                      | primary '.' identifier               // a.b
                      | primary '[' expr ']'                 // a[b]
                      | primary '(' ( expr ( ',' expr )* )? ')'  // a(...)

(* 字面量 *)
literal             ::= int_literal
                      | dec_literal
                      | str_literal
                      | fstring_literal
                      | bool_literal
                      | nil_literal

int_literal         ::= [0-9]+
dec_literal         ::= [0-9]+ '.' [0-9]+
str_literal         ::= '"' ( .*? ) '"'           // 普通/多行字符串
fstring_literal     ::= 'f"' ( .*? | '{' expr '}' )* '"'
bool_literal        ::= 'True' | 'False'
nil_literal         ::= 'Nil'

(* 列表 *)
list_literal        ::= '[' ( expr ( ',' expr )* )? ']'

(* 字典 *)
dict_literal        ::= '{' ( dict_entry ( ',' dict_entry )* )? '}'
dict_entry          ::= expr '=' expr              // {"key" = val, 0 = 2}

(* -------------------------------------------------------------------------- *)
(* Lambda / 匿名函数 *)
(* -------------------------------------------------------------------------- *)
lambda_expr         ::= short_lambda | long_lambda

short_lambda        ::= '|' ( identifier ( ',' identifier )* )? '|' expr

long_lambda         ::= 'fn' '(' ( param_list )? ')' block 'end'

(* -------------------------------------------------------------------------- *)
(* 控制流 *)
(* -------------------------------------------------------------------------- *)
if_stmt             ::= 'if' expr block
                          ( 'else if' expr block )*
                          ( 'else' block )?
                      'end'

while_stmt          ::= 'while' expr block 'end'

for_stmt            ::= 'for' identifier 'in' expr block 'end'
break_stmt          ::= 'break'
next_stmt           ::= 'next'
return_stmt         ::= 'return' ( expr )?

throw_stmt          ::= 'throw' expr

(* -------------------------------------------------------------------------- *)
(* Try / Catch / Ensure *)
(* -------------------------------------------------------------------------- *)
try_stmt            ::= 'try' block ( catch_clause )* 'end'
catch_clause        ::= 'catch' identifier '(' str_literal ')' block

ensure_stmt         ::= 'ensure' expr

(* -------------------------------------------------------------------------- *)
(* 函数定义 *)
(* -------------------------------------------------------------------------- *)
function_def        ::= 'fn' identifier ( '(' param_list ')' | ) block 'end'

param_list          ::= param ( ',' param )*
param               ::= identifier | spread_param
spread_param        ::= '...' identifier

block               ::= statement*

(* -------------------------------------------------------------------------- *)
(* 对象 / 类式定义 *)
(* -------------------------------------------------------------------------- *)
object_def          ::= 'object' identifier ( ':' identifier )? object_body 'end'
object_body         ::= ( object_attr | object_method )*
object_attr         ::= identifier '=' expr
object_method       ::= function_def

(* 对象创建 *)
create_call         ::= 'create' ( '(' expr ')' )?  // create() / create(obj)

(* -------------------------------------------------------------------------- *)
(* 模块系统 *)
(* -------------------------------------------------------------------------- *)
import_stmt         ::= 'import' identifier ( 'at' str_literal )?